apply plugin: 'com.android.application'
apply from: "$rootDir/helpers.gradle"
apply plugin: 'com.getkeepsafe.dexcount'

//Api token can be defined as environment variable or in a file
//Exception message has more specific information where the api token should be placed
final APP_TOKEN_KEY = 'ONFIDO_API_TOKEN'
final STRING_TOKEN_RES_KEY = 'onfido_api_token'
final APP_TOKEN_FILE = 'secrets.properties'
final APP_TOKEN_ENV_VAR_NAME = 'ONFIDO_API_TOKEN'
final ERROR_MESSAGE = "You have not defined an app key for the Onfido api."

def apiKey = getValueFromFileOrEnv(APP_TOKEN_ENV_VAR_NAME, APP_TOKEN_FILE, APP_TOKEN_KEY, ERROR_MESSAGE)

def sdkVersion = '4.0.0'
def versionCodeNumber = 400

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.6.4'
    }
}

dexcount {
    format = "list"
    includeClasses = false
    includeFieldCount = true
    includeTotalMethodCount = true
    orderByMethodCount = false
    verbose = false
    maxTreeDepth = Integer.MAX_VALUE
    teamCityIntegration = false
    enableForInstantRun = false
    teamCitySlug = null
    runOnEachAssemble = true
    maxMethodCount = 120000
}

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.onfido.sampleapp"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode versionCodeNumber
        versionName "$versionCode-sdk:$sdkVersion"
        injectInStringResources(it, STRING_TOKEN_RES_KEY, apiKey)
    }
    lintOptions {
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    splits {
        abi {
            enable true
            reset()
            include 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            universalApk true //generate an additional APK that contains all the ABIs
        }
    }
}

repositories {
    maven { url "https://dl.bintray.com/onfido/maven" }
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "com.onfido.sdk.capture:onfido-capture-sdk:$sdkVersion"
    implementation 'com.android.support:appcompat-v7:27.1.0'
    implementation 'com.android.support:design:27.1.0'
    testImplementation 'junit:junit:4.12'

    implementation ('com.amitshekhar.android:android-networking:1.0.0', {
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
        exclude group: 'com.squareup.okio', module: 'okio'
        exclude group: 'com.google.code.gson', module: 'gson'
    })

    // only required to support TLSv1.2 for devices with Android API lower than 5.x
    implementation ('com.google.android.gms:play-services-base:11.8.0') {
        exclude group: 'com.android.support' // to avoid conflicts with your current support library
    }
}
