apply plugin: 'com.android.application'
apply from: "$rootDir/helpers.gradle"

//Api token can be defined as environment variable or in a file
//Exception message has more specific information where the api token should be placed
final APP_TOKEN_KEY = 'ONFIDO_API_TOKEN'
final STRING_TOKEN_RES_KEY = 'onfido_api_token'
final APP_TOKEN_FILE = 'secrets.properties'
final APP_TOKEN_ENV_VAR_NAME = 'ONFIDO_API_TOKEN'
final ERROR_MESSAGE = "You have not defined an app key for the Onfido api."

def apiKey = getValueFromFileOrEnv(APP_TOKEN_ENV_VAR_NAME, APP_TOKEN_FILE, APP_TOKEN_KEY, ERROR_MESSAGE)

def sdkVersion = '1.0.0'
def versionCodeNumber = 7

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.3'

    defaultConfig {
        applicationId "com.onfido.sampleapp"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode versionCodeNumber
        versionName "$versionCode-sdk:$sdkVersion"
        injectInStringResources(it, STRING_TOKEN_RES_KEY, apiKey)
        multiDexEnabled true
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

repositories {
    mavenLocal()
    maven { url "https://dl.bintray.com/onfido/maven" }
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile "com.onfido.sdk.capture:onfido-capture-sdk:$sdkVersion"
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    testCompile 'junit:junit:4.12'

    /* Integration test dependencies
    * These dependencies are here just to test the integration of the sdk with app that has them.
    */
    compile 'com.github.bumptech.glide:glide:3.7.0'

    //this overrides the sdk play services dependency, without the app crashes, because there can only be one version of google play services
    compile('com.google.android.gms:play-services-vision:10.0.1')
    compile 'com.google.android.gms:play-services-gcm:10.0.1'
    compile 'com.google.android.gms:play-services-analytics:10.0.1'
    compile 'com.google.android.gms:play-services-analytics-impl:10.0.1'
    compile 'com.google.android.gms:play-services-location:10.0.1'
    compile 'com.google.android.gms:play-services-places:10.0.1'
    compile 'com.google.android.gms:play-services-ads-lite:10.0.1'
    compile 'com.google.firebase:firebase-invites:10.0.1'
    compile 'com.google.firebase:firebase-messaging:10.0.1'
    compile 'com.google.firebase:firebase-ads:10.0.1'
    compile 'com.google.firebase:firebase-config:10.0.1'
    compile 'com.google.firebase:firebase-appindexing:10.0.1'
    /*END OF Integration test dependencies*/

    compile 'com.android.support:multidex:1.0.1'
}
