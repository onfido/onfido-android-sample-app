apply plugin: 'com.android.application'
apply from: "$rootDir/helpers.gradle"

//Api token can be defined as environment variable or in a file
//Exception message has more specific information where the api token should be placed
final APP_TOKEN_KEY = 'ONFIDO_API_TOKEN'
final STRING_TOKEN_RES_KEY = 'onfido_api_token'
final APP_TOKEN_FILE = 'secrets.properties'
final APP_TOKEN_ENV_VAR_NAME = 'ONFIDO_API_TOKEN'
final ERROR_MESSAGE = "You have not defined an app key for the Onfido api."

def apiKey = getValueFromFileOrEnv(APP_TOKEN_ENV_VAR_NAME, APP_TOKEN_FILE, APP_TOKEN_KEY, ERROR_MESSAGE)

def versionCodeNumber = 7

android {
    compileSdkVersion compile_sdk_version
    buildToolsVersion build_tools_version

    defaultConfig {
        applicationId "com.onfido.sampleapp"
        minSdkVersion min_sdk_version
        targetSdkVersion target_sdk_version
        versionCode versionCodeNumber
        versionName "$versionCode-sdk:$onfido_sdk_version"
        injectInStringResources(it, STRING_TOKEN_RES_KEY, apiKey)
        multiDexEnabled true
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

repositories {
    mavenLocal()
    maven { url "https://dl.bintray.com/onfido/maven" }
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile "com.onfido.sdk.capture:onfido-capture-sdk:$onfido_sdk_version"
    compile "com.android.support:appcompat-v7:$support_lib_version"
    compile "com.android.support:design:$support_lib_version"
    testCompile "junit:junit:$junit_version"

    /* Integration test dependencies
    * These dependencies are here just to test the integration of the sdk with app that has them.
    */
    compile "com.github.bumptech.glide:glide:$glide_version"

    //this overrides the sdk play services dependency, without the app crashes, because there can only be one version of google play services
    compile("com.google.android.gms:play-services-vision:$play_services_version")
    compile "com.google.android.gms:play-services-gcm:$play_services_version"
    compile "com.google.android.gms:play-services-analytics:$play_services_version"
    compile "com.google.android.gms:play-services-analytics-impl:$play_services_version"
    compile "com.google.android.gms:play-services-location:$play_services_version"
    compile "com.google.android.gms:play-services-places:$play_services_version"
    compile "com.google.android.gms:play-services-ads-lite:$play_services_version"
    compile "com.google.firebase:firebase-invites:$play_services_version"
    compile "com.google.firebase:firebase-messaging:$play_services_version"
    compile "com.google.firebase:firebase-ads:$play_services_version"
    compile "com.google.firebase:firebase-config:$play_services_version"
    compile "com.google.firebase:firebase-appindexing:$play_services_version"
    /*END OF Integration test dependencies*/

    compile "com.android.support:multidex:$multidex_version"
}
